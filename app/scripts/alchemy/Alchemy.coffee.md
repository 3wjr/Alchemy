<!-- For the next release
---
position: 8
title: Anotated Source
---
-->

    """
    Alchemy.js is a graph drawing application for the web.
    Copyright (C) 2014  GraphAlchemist, Inc.

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU Affero General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU Affero General Public License for more details.

    You should have received a copy of the GNU Affero General Public License
    along with this program.  If not, see <http://www.gnu.org/licenses/>.
    lets
    """

    class Alchemy
        constructor: (userConf=null) ->
            @a = @

            @version  = "#VERSION#"
            @get      = new @get @
            @drawing  = 
                DrawEdge : DrawEdge   @
                DrawEdges: DrawEdges  @
                DrawNode : DrawNode   @
                DrawNodes: DrawNodes  @
                EdgeUtils: @EdgeUtils @
                NodeUtils: @NodeUtils @
            
            @layout   = Layout
            @clustering = Clustering
            @models   =
                Node: Node
                Edge: Edge

            @utils        = 
                warnings: warnings
            @visControls  = {}
            @styles       = {}
            @editor       = {}
            @log          = {}

            @currentRelationshipTypes = {}
            @state =
                "interactions": "default"
                "layout": "default"

            @startGraph = @startGraph @
            @generateLayout = @generateLayout @
            @svgStyles = @svgStyles @

            # alchemy._nodes stores a node object as the value with the unique
            # id specified in the GraphJSON.
            @_nodes = {}

            # alchemy._edges stores an array of edges under every unique id.
            # the edge model is a key value pair where the "key" is either
            # 1) an id provided in the GraphJSON data, or 2) an id generated by
            # Alchemy taking the format of "#{source}-#{target}".  
            # The value is an array of edge 'packets', where the length of the array
            # is typically 1.
            @_edges = {}

            @begin userConf if userConf

        begin: (userConf) ->
            # overide configuration with user inputs
            @setConf(userConf)

            if typeof @a.conf.dataSource is 'string'
                d3.json @a.conf.dataSource, @a.startGraph
            else if typeof @a.conf.dataSource is 'object'
                @a.startGraph @a.conf.dataSource
            @

        setConf: (userConf) -> 
            # apply base themes
            if userConf.theme?
                _.merge defaults, @a.themes["#{userConf.theme}"]

            for key, value of userConf
                if key is "clusterColors" 
                    userConf["clusterColours"] = value
                if key is "backgroundColor"
                    userConf["backgroundColour"] = value
                if key is "nodeColor"
                    userConf["nodeColour"] = value

            @a.conf = _.merge defaults, userConf

        allNodes: => _.map @_nodes, (n) -> n.properties
        allEdges: => _.map @_edges, (e) -> e.properties

    root = exports ? this
    root.Alchemy = Alchemy